Program                  ::= Expression*
                           | EOF

Expression               ::= Comment
                           | Block
                           | Statement
                           | Operation
                           
Block                    ::= '{' Expression* '}'

Operation                ::= VariableAssignment
VariableAssignment       ::= ConditionalExpression (('=' | '*=' | '/=' | '%=' | '+=' | '-=' | '<<=' | '>>=' | '&=' | '^=' | '|=' | '?=' | ':=') ConditionalExpression)*
ConditionalExpression    ::= LogicalORExpression ('?' VariableAssignment ':' VariableAssignment)?
LogicalORExpression      ::= LogicalANDExpression ('||' LogicalANDExpression)*
LogicalANDExpression     ::= BitwiseORExpression ('&&' BitwiseORExpression)*
BitwiseORExpression      ::= BitwiseXORExpression ('|' BitwiseXORExpression)*
BitwiseXORExpression     ::= BitwiseANDExpression ('^' BitwiseANDExpression)*
BitwiseANDExpression     ::= EqualityExpression ('&' EqualityExpression)*
EqualityExpression       ::= RelationalExpression (('==' | '!=') RelationalExpression)*
RelationalExpression     ::= ShiftExpression (('<' | '>' | '<=' | '>=') ShiftExpression)*
ShiftExpression          ::= AdditiveExpression (('<<' | '>>') AdditiveExpression)*
AdditiveExpression       ::= PowerExpression (('+' | '-') PowerExpression)*
PowerExpression          ::= MultiplicativeExpression ('**' MultiplicativeExpression)*
MultiplicativeExpression ::= UnaryExpression (('*' | '/' | '%') UnaryExpression)*
UnaryExpression          ::= Primary '++'
                           | Primary '--'
                           | '++' Primary
                           | '--' Primary
                           | '+' Primary
                           | '-' Primary
                           | '~' Primary
                           | '!' Primary
                           | Primary

Primary                  ::= Type? Member
                           | Value
                           | ParenthesizedExpression

Statement                ::= Do
                           | While
                           | For
                           | ForEach
                           | Break
                           | Continue
                           | If
                           | Switch
                           | Try
                           | Test
                           | NamespaceDeclaration
                           | FunctionDeclaration
                           | Return
                           | Export
                           | Import
                           | Include
                           | Global
                           | Local
                           | Throw
                           | EmptyStatement
                           
Do                       ::= 'do' Expression 'while' '(' Expression ')'
While                    ::= 'while' '(' Expression ')' Expression
For                      ::= 'for' '(' Expression ';' Expression ';' Expression ')' Expression
ForEach                  ::= 'foreach' '(' Expression ';' Expression ';' Expression ')' Expression
Break                    ::= 'break'
Continue                 ::= 'continue'
If                       ::= 'if' '(' Expression ')' Expression Else?
Else                     ::= 'else' Expression
Switch                   ::= 'switch' '(' Expression ')' '{' Case+ Default? '}'
Case                     ::= 'case' Expression ':' Expression*
Default                  ::= 'default' ':' Expression*
Try                      ::= 'try' Expression Catch?
Test                     ::= 'test' '(' Expression? (';' Expression? (';' Expression?)?)? ')' Expression Catch?
Catch                    ::= 'catch' '(' Expression ')' Expression
NamespaceDeclaration     ::= Identifier ('.' Identifier)* Block
FunctionDeclaration      ::= Identifier ('.' Identifier)* '(' Arguments? ')' '=' Expression
                           | Identifier ('.' Identifier)* '(' Arguments? ')' '?=' Block
                           | Identifier ('.' Identifier)* '(' Arguments? ')' '#=' Block
                           | Identifier ('.' Identifier)* '(' Arguments? ')' ':=' Block
                           | Type? Identifier ('.' Identifier)* '(' Arguments? ')' Block
                           | Type? Identifier ('.' Identifier)* '(' Arguments? ')' Script
Return                   ::= 'return' Expression
Export                   ::= 'export' Expression
Import                   ::= 'import' Expression
Include                  ::= 'include' Expression
Global                   ::= 'global' Expression
Local                    ::= 'local' Expression
Throw                    ::= 'throw' Expression
EmptyStatement           ::= ';'

Type                     ::= 'i32'
                           | 'i64'
                           | 'f32'
                           | 'f64'

Arguments                ::= (Expression) (',' Expression)*

Member                   ::= Identifier ('.' Identifier)* '(' Arguments? ')'
                           | Identifier ('.' Identifier)* ('[' Arguments? ']')*

Array                    ::= '{' Element? (',' Element)* '}'
Matrix                   ::= '[' Row? (';' Row)* ']'

Element                  ::= (Key ':')? Expression
Key                      ::= Identifier
                           | String

Row                      ::= Column (',' Column)*
Column                   ::= Expression

ParenthesizedExpression  ::= '(' Expression ')'

Value                    ::= Integer
                           | Real
                           | Complex
                           | Character
                           | String
                           | Array
                           | Matrix
                           | Null
                           | True
                           | False

WhiteSpace               ::= WhiteSpace^token
                         /* ws: definition */

<?TOKENS?>

EOF                      ::= $

Identifier               ::= (FirstIdentifierChar (IdentifierChar)*) - ReservedWord

ReservedWord             ::= 'break'
                           | 'case'
                           | 'catch'
                           | 'continue'
                           | 'default'
                           | 'do'
                           | 'else'
                           | 'export'
                           | 'f32'
                           | 'f64'
                           | 'false'
                           | 'for'
                           | 'foreach'
                           | 'global'
                           | 'i32'
                           | 'i64'
                           | 'if'
                           | 'import'
                           | 'include'
                           | 'local'
                           | 'return'
                           | 'null'
                           | 'switch'
                           | 'test'
                           | 'throw'
                           | 'true'
                           | 'try'
                           | 'while'

Null                     ::= 'null'
True                     ::= 'true'
False                    ::= 'false'

FirstIdentifierChar      ::= '$' | '_' | [A-Za-z]
IdentifierChar           ::= '$' | '_' | [0-9] | [A-Za-z]

Character                ::= "'" SingleCharacter "'"
                           | "'" EscapeSequence "'"
SingleCharacter          ::= InputCharacter - ( "'" | '\' )

String                   ::= '"' StringCharacters? '"'
StringCharacters         ::= StringCharacter+
StringCharacter          ::= InputCharacter - ( '"' | '\' )
                           | EscapeSequence
EscapeSequence           ::= BackSlash 'b'
                           | BackSlash 't'
                           | BackSlash 'n'
                           | BackSlash 'f'
                           | BackSlash 'r'
                           | BackSlash '"'
                           | BackSlash "'"
                           | BackSlash BackSlash
                           | OctalEscape
                           | HexEscapeSequence
OctalEscape              ::= '\' OctalDigit+
HexEscapeSequence        ::= '\x' HexDigit+
OctalDigit               ::= '0'
                           | '1'
                           | '2'
                           | '3'
                           | '4'
                           | '5'
                           | '6'
                           | '7'
HexDigit                 ::= [0-9a-fA-F]
BackSlash                ::= '\' ( UnicodeMarker '005' [cC] )?
InputCharacter           ::= UnicodeInputCharacter - ( CR | LF )

UnicodeInputCharacter    ::= UnicodeEscape
                           | RawInputCharacter
UnicodeEscape            ::= '\' UnicodeMarker HexDigit HexDigit HexDigit HexDigit
UnicodeMarker            ::= 'u' | 'U'
RawInputCharacter        ::= [#x0001-#xD7FF]
                           | [#xE000-#xFFFD]
                           | [#x10000-#x10FFFF]

Integer                  ::= '0'
                           | Digit+ ('u' | 'U')? ('l' | 'll' | 'L' | 'LL' | 'f' | 'F')?
                           | Octal
                           | Hexadecimal
Complex                  ::= Real? Imaginary
Real                     ::= '-'? Digit+ '.' Digit+ (('e' | 'E' | 'p' | 'P') ('+' | '-')? Digit+)?
Imaginary                ::= (('+' | '-')? Real '*' 'i')

Digit                    ::= [0-9]
Octal                    ::= '0' OctalDigit+
Hexadecimal              ::= '0x' HexDigit+

Comment                  ::= SingleLineComment
                           | MultiLineComment

SingleLineComment        ::= '//' (SourceCharacter - LineTerminator)*
                           | '#'  (SourceCharacter - LineTerminator)*
MultiLineComment         ::= '/*' (SourceCharacter* - (SourceCharacter* '*/' SourceCharacter*)) '*/'

Script                   ::= '/{' (SourceCharacter* - (SourceCharacter* '}/' SourceCharacter*)) '}/'

SourceCharacter          ::= .

LineTerminator           ::= LF
                           | CR
                           | CR LF
LF                       ::= #x000A
CR                       ::= #x000D

WhiteSpace               ::= [#x0009#x000A#x000D#x0020]+
